plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    flatDir {
        dirs 'libs'
    }
    maven {
        name = 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'essentialsx'
        url = 'https://repo.essentialsx.net/releases/'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':spigot-api')
    // Spigot
    shadow 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.google.guava:guava:31.0.1-jre'

    // Annotations
    shadow 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.jetbrains:annotations:22.0.0'

    // Other plugins
    shadow 'com.github.PikaMug:Quests:4.0.7'
    shadow 'com.github.whimc:Portals:2.8.3'

    // Lombok
    shadow 'org.projectlombok:lombok:1.18.22'
    testImplementation('org.projectlombok:lombok:1.18.22')
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

//    implementation 'javax.visrec:visrec-api:1.0.5'
//    implementation 'com.opencsv:opencsv:5.5.2'

    implementation 'org.bstats:bstats-bukkit:3.0.0'

    shadow fileTree(dir: "../libs", includes: ['*.jar'])
    implementation fileTree(dir: '../../mantle/bukkit/build/libs', include: '*.jar')

    implementation "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation 'net.kyori:adventure-text-serializer-legacy:4.11.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.11.0'

    // Integrations
    shadow 'net.essentialsx:EssentialsX:2.19.7'

}

shadowJar {
    archiveFileName = "Journey-Spigot-${project.version}.jar"

    dependencies {
        include(dependency('me.pietelite.journey:.*'))
        include(dependency('me.pietelite.mantle:.*'))
        include(dependency('org.antlr:.*'))
        include(dependency('net.kyori:.*'))
        include(dependency('org.bstats:.*'))
        include(dependency('org.apache.httpcomponents:.*'))
    }

    relocate 'me.pietelite.mantle', 'me.pietelite.journey.libs.mantle'
    relocate 'org.antlr', 'me.pietelite.journey.libs.antlr'
    relocate 'org.stringtemplate', 'me.pietelite.journey.libs.stringtemplate'
    relocate 'net.kyori', 'me.pietelite.journey.libs.kyori'
    relocate 'org.bstats', 'me.pietelite.journey.libs.bstats'
    relocate 'org.apache.httpcomponents', 'me.pietelite.journey.libs.http'
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'journey'
            groupId = project.group
            version = project.version

            from components.java
        }
    }
}
